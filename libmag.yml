

MAGBASE:
    calls:
        - MAGLOAD
        - LSXCALLS
    initialize_code: |
        LD	A,(000BH)
        LD	(SMC01+2),A
        LD	(SMC02+2),A
        LD	(SMC03+2),A
        LD	(SMC04+2),A
        LD	(SMC05+2),A
        LD	(SMC06+2),A
        LD	(SMC07+2),A
        LD	(SMC08+2),A
        LD	(SMC09+2),A
        LD	(SMC10+2),A
        LD	(SMC11+2),A
        LD	(SMC12+2),A
        LD	(SMC13+2),A
        LD	(SMC14+2),A
        LD	(SMC15+2),A
        LD	(SMC16+2),A
        LD	(SMC17+2),A
        LD	(SMC18+2),A
        LD	(SMC19+2),A
        RET

    code: |
        ;
        ;	MAG  by Gaku
        ;
        THOLD	EQU	11
        ; MAG_WIDTH	EQU	40
        ;
        OSFLG	EQU	$0003
        SYSTEM	EQU	$0005
        JP_HL	EQU	$000F
        BIOS	EQU	$0018
        DTA	EQU	$0080

        FLAGBF	EQU	((__WORKEND__ + 255) & 0xFF00)
        LINEBF	EQU	FLAGBF+256

        KWABF1:	EQU	LINEBF+16*512
        KWABF2:	EQU	KWABF1+2048

        BUFSIZ	EQU	4*1024
        DTBUF	EQU	KWABF2+2048
        DTBUFE	EQU	DTBUF+BUFSIZ

        PDSIZE	EQU	8*1024
        PDBUF	EQU	DTBUFE
        PDBUFE	EQU	PDBUF+PDSIZE

        HEADBF	EQU	PDBUFE
        PALBF	EQU	HEADBF+32
    

GRDISP:
    code: |
        LD A,H
        OR L
        JR Z,GR_NODISP

        ; Z 互換(8色)モード
        LD	BC,$1FB0
        DB	$ED,$71

        ; $10xxに$AA(BLUE)
        LD	BC,$10AA
        OUT	(C),C
        ; $11xxに$CC(RED)
        LD	BC,$11CC
        OUT	(C),C
        ; $12xxにF0(GREEN)
        LD	BC,$12F0
        OUT	(C),C

        ; ; $13xxに0(描画優先度設定)→とりあえず触らない
        ; INC	B
        ; DB	$ED,$71

        ; $1FD0は触らないでおく
        RET

        GR_NODISP:

        ; パレット設定を全て0にして非表示にする
        LD	B,$10
        DB	$ED,$71
        INC	B
        DB	$ED,$71
        INC	B
        DB	$ED,$71
        INC	B
        DB	$ED,$71

        ; $1FD0は触らないでおく
        RET


GRCLS:
    calls:
        - MAGBASE
    code: |
        SMC16:	LD	A,($ECBF)
        RES	4,A
        CALL	CLS0
        SMC17:	LD	A,($ECBF)
        SET	4,A
        CLS0:
        LD	BC,$1FD0
        OUT	(C),A
        SMC18:	LD	($ECBF),A
        XOR	A
        LD	C,A
        LD	HL,$4003
        CLS1:
        LD	B,H
        CLS2:
        DB	$ED,$71
        INC	B
        JR	NZ,CLS2
        ;
        ADD	A,L
        LD	C,A
        JR	NZ,CLS1
        RET

MAGLOAD:
    calls:
        - MAGBASE
        - X1WORK
        - GRCLS
        - MULHLDE
    code: |
        ; HL = FILENAME, DE = X, BC = Y


        ; X,YからVRAM位置を算出する
        ; 200line専用
        ; LD	BC,X/8 + ((Y & 7) << 11) + ((Y/8)*80)
        PUSH HL
        PUSH DE
        PUSH BC
        LD A,C
        AND 7
        SLA A
        SLA A
        SLA A
        LD B,A
        LD C,0
        POP HL
        PUSH BC
        ; HL = HL / 8
        SRA H
        RR L
        SRA H
        RR L
        SRA H
        RR L
        LD DE,80
        CALL MULHLDE
        POP BC
        ADD HL,BC
        POP DE
        ; DE = DE / 8
        SRA D
        RR E
        SRA D
        RR E
        SRA D
        RR E
        ADD HL,DE
        LD (MAG_STARTPOS+1),HL
        POP HL

        ; 機種チェックはとりあえずしない
        ; LD	HL,(OSFLG)
        ; LD	A,H
        ; CP	$1D
        ; RET	NZ
        ; LD	A,L
        ; AND	$7F
        ; RET	NZ

        EX HL,DE

        LD HL,FCB1
        LD C,$29  ; _PPATH
        CALL BDOS

        LD	(MAG_SPBK),SP

        ; LD	SP,(SYSTEM+1)
        ; LD	HL,FCB1+1
        ; ファイル名直接指定のみに対応
        ; LD	A,(HL)
        ; CP	$20
        ; JR	NZ,MAGLOAD_ALL2
        ; LD	B,8
        ; MAGLOAD_ALL1:
        ; LD	(HL),"?"
        ; INC	HL
        ; DJNZ	MAGLOAD_ALL1
        ; MAGLOAD_ALL2:
        ; ;拡張子 MAG
        ; LD	A,(FCB1+9)
        ; CP	$20
        ; JR	NZ,MAGLOAD_EXT1
        ; LD	HL,"A"*256+"M"
        ; LD	(FCB1+9),HL
        ; LD	A,"G"
        ; LD	(FCB1+11),A
        ; MAGLOAD_EXT1:
        ; LD	DE,DTA+1

        MAG_LOAD:
        LD	DE,MAGLOAD_FILE
        LD	C,$1A
        CALL	SYSTEM

        LD	DE,FCB1
        LD	C,$11
        CALL	SYSTEM
        OR	A
        JP	NZ,END

        ; CALL	MAGLOAD_GRAPH1
        MAGLOAD_WILD:
        LD	DE,MAGLOAD_FILE
        LD	C,$0F
        CALL	SYSTEM
        OR	A
        JP	NZ,END

        LD	HL,0
        LD	(MAGLOAD_FILE+33),HL
        LD	(MAGLOAD_FILE+35),HL
        INC	L
        LD	(MAGLOAD_FILE+14),HL

        LD	DE,DTBUF
        LD	C,$1A
        CALL	SYSTEM

        LD	HL,FLAGBF
        LD	DE,FLAGBF+1
        LD	BC,16*512+256+4096-1
        LD	(HL),0
        LDIR

        LD	A,1
        LD	(FLAGAX+1),A
        LD	HL,0
        LD	(ZURI+1),HL

        LD	HL,LINEBF
        LD	(LINEX+1),HL

        LD	HL,$FFFF
        LD	DE,$FFFF
        MAG_STARTPOS: LD	BC,0
        EXX

        LD	HL,HEADBF
        LD	BC,8
        CALL	READ
        LD	HL,HEADBF
        LD	DE,MAGID
        LD	B,8
        CALL	CPSTR
        JP	NZ,NEXT

        ; LD	E,$0C
        ; LD	C,2
        ; CALL	SYSTEM

        ; 画面クリアするかどうか(しない)
        ; CLSSW:	LD	A,$00
        ; OR	A
        ; CALL	NZ,CLS

        LD	BC,$1800
        LD	A,12
        OUT	(C),A
        INC	C
        DB	$ED,$71
        DEC	C
        INC	A
        OUT	(C),A
        INC	C
        DB	$ED,$71
        LD	HL,8
        COMMENT:
        INC	HL
        CALL	FGETC
        CP	$1A
        JR	Z,HEADER
        ; LD	E,A
        ; LD	C,2
        ; CALL	SYSTEM
        JR	COMMENT
        HEADER:
        LD	(ADDHD+1),HL
        ; LD	E,$0D
        ; LD	C,2
        ; CALL	SYSTEM
        ; LD	E,$0A
        ; LD	C,2
        ; CALL	SYSTEM

        CALL	FGETC
        OR	A
        JP	NZ,NEXT
        LD	HL,HEADBF
        LD	(HL),A
        INC	HL
        LD	BC,31+48	;ヘッダ パレット
        CALL	READ
        PUSH	HL

        LD	HL,PALBF
        LD	DE,DEGI8
        LD	B,48
        CALL	CPSTR
        LD	A,(HEADBF+3)	;スクリーンモード
        JR	NZ,NOTD
        OR	6
        LD	(HEADBF+3),A
        NOTD:
        BIT	7,A		;256色
        JP	NZ,NEXT
        AND	1		;200ライン
        LD	(L200+1),A

        LD	HL,(HEADBF+16)
        LD	BC,(HEADBF+12)
        AND	A
        SBC	HL,BC
        LD	B,H
        LD	C,L
        POP	HL
        CALL	READ		;フラグA
        			;フラグB
        LD	HL,MAGLOAD_FILE
        LD	DE,MAGLOAD_FILE2
        LD	BC,37
        LDIR

        LD	A,(HEADBF+26)
        LD	HL,(HEADBF+24)
        ADDHD:	LD	DE,0
        ADD	HL,DE
        ADC	A,0
        LD	(MAGLOAD_FILE2+33),HL
        LD	(MAGLOAD_FILE2+35),A

        LD	HL,(HEADBF+8)
        LD	BC,(HEADBF+4)
        AND	A
        SBC	HL,BC
        LD	BC,8
        ADD	HL,BC
        ADD	HL,HL
        ADD	HL,HL
        ADD	HL,HL
        ADD	HL,HL
        ADD	HL,HL
        LD	A,H
        LD	(XSIZE+1),A

        LD	HL,(HEADBF+10)
        LD	BC,(HEADBF+6)
        AND	A
        SBC	HL,BC
        INC	HL
        LD	(YSIZE+1),HL

        RGBSW:	LD	B,$00
        LD	A,(HEADBF+3)
        OR	B
        LD	(HEADBF+3),A

        LD	HL,KUWANO
        LD	DE,KUWANO

        SMC01:	LD	A,($ECBF)
        RRCA
        JR	NC,LOW
        			;High Reso
        LD	A,(HEADBF+3)	;スクリーンモード
        LD	B,0
        AND	6
        CP	6
        JR	NZ,HIGH3
        			;デジタル 8 色
        LD	A,(HEADBF+3)	;スクリーンモード
        LD	DE,RGB

        ADD	A,A
        AND	2
        JR	Z,HIGH2

        LD	DE,NUL
        HIGH2:
        LD	HL,RGB
        HIGH3:
        LD	B,A
        SMC02:	LD	A,($ECBF)
        AND	$FD
        OR	B
        SMC03:	LD	($ECBF),A
        LD	BC,$1FD0
        OUT	(C),A

        JR	FA

        LOW:
        LD	HL,KUWANO2
        LD	DE,NUL

        LD	A,(HEADBF+3)	;スクリーンモード
        AND	7
        CP	7
        JR	NZ,FA

        LD	HL,RGB
        FA:
        LD	(MODE1+1),HL
        LD	(MODE2+1),DE
        ;
        			;パレット 8bit->4bit
        LD	B,48
        LD	HL,PALBF
        PALI:
        LD	A,(HL)
        RRCA
        RRCA
        RRCA
        RRCA
        AND	$0F
        SUB	4
        JR	NC,PLUS
        XOR	A
        PLUS:
        LD	(HL),A
        INC	HL
        DJNZ	PALI

        LD	IX,(HEADBF+12)	;フラグA
        LD	DE,HEADBF
        ADD	IX,DE
        LD	IY,(HEADBF+16)	;フラグB
        ADD	IY,DE

        LD	B,25
        LOOP1:
        PUSH	BC

        ; LD	E,$FF
        ; LD	C,6
        ; CALL	SYSTEM
        ; CP	$1B
        ; JP	Z,NEXT
        ; CP	3
        ; JP	Z,END
        ; CP	$13
        ; CALL	Z,WAIT1

        ZURI:	LD	HL,0
        LD	A,H
        OR	L
        JR	Z,ZURI1

        LD	BC,$1800
        LD	A,12
        OUT	(C),A
        INC	C
        OUT	(C),H
        DEC	C
        INC	A
        OUT	(C),A
        INC	C
        OUT	(C),L
        DEC	C
        LD	A,6
        OUT	(C),A
        INC	C
        LD	A,24
        OUT	(C),A
        ; LD	BC,MAG_WIDTH
        LD A,(AT_WIDTH)
        LD C,A
        LD B,0
        ADD	HL,BC
        LD	(ZURI+1),HL
        ZURI1:
        LD	B,8
        LOOP2:
        PUSH	BC
        EXX
        PUSH	BC
        PUSH	BC
        EXX
        SMC04:	LD	A,($ECBF)
        AND	$EF
        SMC05:	LD	($ECBF),A
        LD	BC,$1FD0
        OUT	(C),A

        CALL	MAG
        LD	HL,(LINEX+1)
        PUSH	HL
        L200:	LD	A,$00
        OR	A
        CALL	Z,MAG
        LD	HL,(LINEX+1)
        EX	(SP),HL
        MODE1:	CALL	KUWANO

        SMC06:	LD	A,($ECBF)
        OR	$10
        SMC07:	LD	($ECBF),A
        LD	BC,$1FD0
        OUT	(C),A

        POP	HL
        EXX
        POP	BC
        EXX
        MODE2:	CALL	KUWANO

        EXX
        POP	BC
        LD	A,B
        ADD	A,8
        LD	B,A
        EXX

        POP	BC
        DEC	B
        JP	NZ,LOOP2

        LD	HL,(YSIZE+1)
        LD	A,H
        OR	L
        JR	Z,WAIT

        EXX
        PUSH	HL
        ; LD	HL,MAG_WIDTH
        LD A,(AT_WIDTH)
        LD L,A
        LD H,0
        ADD	HL,BC
        LD	A,H
        AND	7
        LD	B,A
        LD	C,L
        POP	HL
        EXX
        POP	BC
        DEC	B
        JP	NZ,LOOP1

        CALL	WAIT1
        LD	HL,(ZURI+1)
        ; LD	BC,MAG_WIDTH
        LD A,(AT_WIDTH)
        LD C,A
        LD B,0
        ADD	HL,BC
        LD	(ZURI+1),HL

        LD	BC,$2000
        LD	HL,$800
        CLRAT:
        DB	$ED,$71
        INC	BC
        DEC	HL
        LD	A,H
        OR	L
        JR	NZ,CLRAT

        LD	B,25
        JP	LOOP1

        WAIT:
        DB	$3E
        NEXT:
        XOR	A
        OR	A
        EX	AF,AF'
        ; LD	SP,(SYSTEM+1)

        ; LD	DE,MAGLOAD_FILE
        ; LD	C,$1A
        ; CALL	SYSTEM

        ; LD	C,$12
        ; CALL	SYSTEM
        ; OR	A
        ; EX	AF,AF'
        ; CALL	NZ,WAIT1
        ; CALL	WAIT2
        ; EX	AF,AF'
        ; JP	Z,MAGLOAD_WILD

        ; ?????
        POP HL

        END:
        CALL	MOTOFF
        ; CALL	GRAPH0

        LD L,0
        LD H,L
        ; LD	HL,(MAG_SPBK)
        ; LD SP,HL
        RET

        WAIT1:
        CALL	MOTOFF
        LD	BC,$1800
        LD	A,6
        OUT	(C),A
        INC	C
        LD	A,25
        OUT	(C),A

        CALL	WAIT2
        WAIT3:
        LD	E,$FF
        LD	C,6
        CALL	SYSTEM
        OR	A
        JR	Z,WAIT3
        CP	3
        JR	Z,END
        RET

        WAIT2:
        LD	E,$FF
        LD	C,6
        CALL	SYSTEM
        OR	A
        JR	NZ,WAIT2

        SMC08:	LD	A,($EC92)
        OR	A
        JR	NZ,WAIT2
        RET

        RGB:
        ; LD	C,MAG_WIDTH
        ; LD A,(AT_WIDTH)
        LD A,(XSIZE+1)
        LD C,A
        DIS1:
        LD	DE,$4004
        CALL	DISS		;G
        EXX
        SET	7,B
        SET	6,B
        OUT	(C),A
        EXX
        LD	DE,$2002
        CALL	DISS		;R
        EXX
        RES	6,B
        OUT	(C),A
        EXX
        LD	DE,$1001
        CALL	DISS		;B
        EXX
        RES	7,B
        SET	6,B
        OUT	(C),A
        LD	A,B
        INC	BC
        XOR	B
        AND	8
        JR	Z,INCXX
        LD	A,B
        SUB	8
        LD	B,A
        INCXX:
        EXX
        INC	HL
        INC	HL
        INC	HL
        INC	HL
        DEC	C
        JR	NZ,DIS1

        DISS:
        PUSH	BC
        PUSH	HL
        LD	BC,$0400
        DISS1:
        LD	A,(HL)
        AND	D
        JR	Z,DISS2
        SCF
        DISS2:
        RL	C
        LD	A,(HL)
        AND	E
        JR	Z,DISS3
        SCF
        DISS3:
        RL	C

        INC	HL
        DJNZ	DISS1

        LD	A,C
        POP	HL
        POP	BC
        RET

        KUWANO:
        PUSH	IX
        PUSH	IY

        KLINE1:	LD	IX,KWABF1+4
        KLINE2:	LD	IY,KWABF2+4

        LD	B,80
        KUWA1:
        PUSH	BC

        LD	B,4
        KUWA2:
        PUSH	BC

        LD	A,(HL)
        INC	HL
        PUSH	HL

        PUSH	AF
        RRCA
        RRCA
        RRCA
        RRCA
        CALL	PALET1
        POP	AF
        CALL	PALET1

        POP	HL
        POP	BC
        DJNZ	KUWA2

        EXX
        SET	7,B
        SET	6,B
        LD	A,(MAGLOAD_GREEN)
        OUT	(C),A
        RES	6,B
        LD	A,(MAGLOAD_RED)
        OUT	(C),A
        RES	7,B
        SET	6,B
        LD	A,(MAGLOAD_BLUE)
        OUT	(C),A
        LD	A,B
        INC	BC
        XOR	B
        AND	8
        JR	Z,INCX
        LD	A,B
        SUB	8
        LD	B,A
        INCX:
        EXX
        POP	BC
        DJNZ	KUWA1
        KUWAE:
        LD	HL,(KLINE1+2)
        LD	IX,(KLINE2+2)
        LD	(KLINE2+2),HL
        LD	(KLINE1+2),IX

        POP	IY
        POP	IX
        RET

        PALET1:
        ;C=GREEN D=RED E=BLUE
        AND	$0F
        LD	E,A
        ADD	A,A
        ADD	A,E
        LD	E,A
        LD	D,0
        LD	HL,PALBF
        ADD	HL,DE
        LD	A,(HL)
        INC	HL
        LD	D,(HL)
        INC	HL
        LD	E,(HL)
        PALETX:
        LD	HL,MAGLOAD_GREEN
        CALL	KUWANO1
        LD	A,D
        LD	HL,MAGLOAD_RED		;RED
        CALL	KUWANO1
        LD	A,E
        LD	HL,MAGLOAD_BLUE		;BLUE
        KUWANO1:
        ADD	A,(IX+0)		;桑野
        LD	(IX+0),0
        ADD	A,-THOLD
        JR	C,PSET
        SUB	-THOLD
        PSET:
        RL	(HL)

        LD	B,A
        SRL	B
        SUB	B
        LD	C,B		;C=A/2
        SRL	B
        SUB	B
        PUSH	BC		;B=A/4
        SRL	B
        SUB	B
        PUSH	BC		;B=A/8

        ADD	A,(IY+0)		;Y+1
        LD	(IY+0),A

        POP	AF		;A=A/8
        ADD	A,(IY+3*1)	;Y+1 X+1
        LD	(IY+3*1),A

        POP	AF		;A=A/4
        ADD	A,(IY-3*1)	;Y+1 X-1
        LD	(IY-3*1),A

        LD	A,C		;A=A/2
        ADD	A,(IX+3*1)	;X+1
        LD	(IX+3*1),A

        INC	IX
        INC	IY
        NUL:
        RET

        KUWANO2:
        POP	BC
        POP	DE
        PUSH	DE
        PUSH	BC

        PUSH	IX
        PUSH	IY

        LD	IX,(KLINE1+2)
        LD	IY,(KLINE2+2)

        LD	B,80
        LKUWA1:
        PUSH	BC

        LD	B,4
        LKUWA2:
        PUSH	BC

        LD	A,(DE)
        INC	DE
        LD	B,(HL)
        INC	HL
        PUSH	DE
        PUSH	HL

        PUSH	BC
        PUSH	AF
        LD	A,B
        RRCA
        RRCA
        RRCA
        RRCA
        LD	B,A
        POP	AF
        PUSH	AF
        RRCA
        RRCA
        RRCA
        RRCA
        CALL	PALET2
        POP	AF
        POP	BC
        CALL	PALET2

        POP	HL
        POP	DE
        POP	BC
        DJNZ	LKUWA2

        EXX
        SET	7,B
        SET	6,B
        LD	A,(MAGLOAD_GREEN)
        OUT	(C),A
        RES	6,B
        LD	A,(MAGLOAD_RED)
        OUT	(C),A
        RES	7,B
        SET	6,B
        LD	A,(MAGLOAD_BLUE)
        OUT	(C),A
        LD	A,B
        INC	BC
        XOR	B
        AND	8
        JR	Z,LINCX
        LD	A,B
        SUB	8
        LD	B,A
        LINCX:
        EXX
        POP	BC
        DJNZ	LKUWA1
        JP	KUWAE

        PALET2:
        ;C=GREEN D=RED E=BLUE
        AND	$0F
        LD	E,A
        ADD	A,A
        ADD	A,E
        LD	E,A
        LD	D,0
        LD	HL,PALBF
        ADD	HL,DE
        LD	C,(HL)
        INC	HL
        LD	D,(HL)
        INC	HL
        LD	E,(HL)

        LD	A,B
        AND	$0F
        PUSH	DE
        LD	E,A
        ADD	A,A
        ADD	A,E
        LD	E,A
        LD	D,0
        LD	HL,PALBF
        ADD	HL,DE
        POP	DE

        LD	A,(HL)
        INC	HL
        ADD	A,C
        RRA
        LD	C,A

        LD	A,(HL)
        INC	HL
        ADD	A,D
        RRA
        LD	D,A

        LD	A,(HL)
        ADD	A,E
        RRA
        LD	E,A

        LD	A,C
        JP	PALETX

        ;				フラグ&ピクセル 展開
        MAG:
        LD	A,(LINEX+2)
        SUB	LINEBF/256-2
        AND	$1F
        ADD	A,LINEBF/256
        LD	(LINEX+2),A

        LINEX:	LD	DE,LINEBF
        XSIZE:	LD	B,80
        YSIZE:	LD	HL,0

        LD	A,H
        OR	L
        JR	Z,CLEAR
        DEC	HL
        LD	(YSIZE+1),HL

        LD	HL,FLAGBF
        FLAGA1:
        RLC	(IX+0)
        JR	C,FLAGA3
        XOR	A
        JR	FLAGA4
        FLAGA3:
        CALL	FGETC
        FLAGA4:
        XOR	(HL)
        LD	(HL),A
        CP	$11
        JR	NZ,FLAGA5
        PUSH	BC
        PUSH	HL
        LD	H,D
        LD	L,E
        DEC	HL
        DEC	HL
        LDI
        LDI
        LDI
        LDI
        POP	HL
        POP	BC
        JR	FLAGA6
        FLAGA5:
        RRCA
        RRCA
        RRCA
        CALL	PIC
        LD	A,(HL)
        ADD	A,A
        CALL	PIC
        FLAGA6:
        INC	HL
        FLAGAX:	LD	A,$01
        RLCA
        LD	(FLAGAX+1),A
        JR	NC,FLAGA2
        INC	IX
        FLAGA2:
        DJNZ	FLAGA1
        RET

        CLEAR:
        LD	C,4
        CLEAR1:
        LD	(DE),A
        INC	DE
        DJNZ	CLEAR1
        DEC	C
        JR	NZ,CLEAR1
        RET

        PIC:
        AND	$1E
        JR	NZ,PICT

        CALL	FGETP
        LD	(DE),A
        INC	DE
        CALL	FGETP
        LD	(DE),A
        INC	DE
        RET
        PICT:
        PUSH	BC
        PUSH	HL
        LD	C,A
        LD	B,0
        LD	HL,MAGLOAD_TABLE
        ADD	HL,BC

        LD	A,E
        SUB	(HL)		;1
        INC	HL
        LD	H,(HL)
        LD	L,A
        LD	A,D
        SBC	A,LINEBF/256	;2
        SUB	H
        AND	$1F
        ADD	A,LINEBF/256
        LD	H,A

        LDI
        LDI
        POP	HL
        POP	BC
        RET

        READ:
        PUSH	HL
        ADD	HL,BC
        JP	C,END
        LD	DE,(SYSTEM+1)
        DEC	D
        SBC	HL,DE
        JP	NC,NEXT
        POP	HL
        READ1:
        LD	A,B
        OR	C
        RET	Z
        CALL	FGETC
        LD	(HL),A
        INC	HL
        DEC	BC
        JR	READ1

        FGETC:
        EXX
        LD	A,H
        CP	DTBUFE/$100
        JR	NC,FGETC2
        FGETC1:
        LD	A,(HL)
        INC	HL
        EXX
        RET
        FGETC2:
        PUSH	BC
        PUSH	DE
        LD	DE,DTBUF
        LD	C,$1A
        CALL	SYSTEM
        LD	DE,MAGLOAD_FILE
        LD	HL,BUFSIZ
        LD	C,$27
        CALL	SYSTEM
        ADD	A,$FE
        JP	C,NEXT

        SMC09:	LD	A,($EC88)
        PUSH	IX
        SMC10:	CALL	$ECDC
        LD	A,(IX+$12)
        POP	IX
        AND	$7F
        CP	7
        JR	NZ,FGETC3
        SMC11:	LD	A,($EC84)
        OR	$80
        LD	BC,$FFC
        OUT	(C),A
        FGETC3:
        LD	A,H
        OR	L
        JP	Z,WAIT
        LD	HL,DTBUF
        POP	DE
        POP	BC
        JR	FGETC1

        FGETP:
        EXX
        LD	A,D
        CP	PDBUFE/$100
        JR	NC,FGETP2
        FGETP1:
        LD	A,(DE)
        INC	DE
        EXX
        RET
        FGETP2:
        PUSH	BC
        PUSH	HL
        LD	DE,PDBUF
        LD	C,$1A
        CALL	SYSTEM
        LD	DE,MAGLOAD_FILE2
        LD	HL,PDSIZE
        LD	C,$27
        CALL	SYSTEM
        ADD	A,$FE
        JP	C,NEXT

        SMC12:	LD	A,($EC88)
        PUSH	IX
        SMC13:	CALL	$ECDC
        LD	A,(IX+$12)
        POP	IX
        AND	$7F
        CP	7
        JR	NZ,FGETP3
        SMC14:	LD	A,($EC84)
        OR	$80
        LD	BC,$FFC
        OUT	(C),A
        FGETP3:
        LD	A,H
        OR	L
        JP	Z,WAIT
        LD	DE,PDBUF
        POP	HL
        POP	BC
        JR	FGETP1

        MOTOFF:
        SMC15:	LD	A,($EC84)
        AND	$7F
        LD	BC,$FFC
        OUT	(C),A
        RET

        CPSTR:
        LD	A,(DE)
        INC	DE
        CP	(HL)
        INC	HL
        RET	NZ
        DJNZ	CPSTR
        RET


        SWITCH:
        LD	A,(DE)
        CP	$20
        RET	C
        INC	DE
        CP	"/"
        JR	NZ,SWITCH
        LD	A,(DE)
        INC	DE
        CAP:
        CP	"a"
        RET	C
        CP	"z"+1
        RET	NC
        SUB	$20
        RET

        GRAPH0:
        LD	B,$10
        DB	$ED,$71
        INC	B
        DB	$ED,$71
        INC	B
        DB	$ED,$71
        INC	B
        DB	$ED,$71
        LD	A,(BIOS)
        INC	A
        RET	Z
        GR1:
        CALL	G1FD0
        OR	$80
        JR	GR3
        MAGLOAD_GRAPH1:
        LD	BC,$1FB0
        DB	$ED,$71
        LD	BC,$10AA
        OUT	(C),C
        LD	BC,$11CC
        OUT	(C),C
        LD	BC,$12F0
        OUT	(C),C
        INC	B
        DB	$ED,$71
        CALL	G1FD0
        AND	$77
        GR3:
        LD	(HL),A
        LD	BC,$1FD0
        OUT	(C),A
        RET
        G1FD0:
        SMC19:	LD	HL,$ECBF
        LD	A,(HL)
        RET

        MAGLOAD_TABLE:
        DB	0  ,0		; 0
        DB	1*2,0		; 1
        DB	2*2,0		; 2
        DB	4*2,0		; 3
        DB	0  ,1*2		; 4
        DB	1*2,1*2		; 5
        DB	0  ,2*2		; 6
        DB	1*2,2*2		; 7
        DB	2*2,2*2		; 8
        DB	0  ,4*2		; 9
        DB	1*2,4*2		;10
        DB	2*2,4*2		;11
        DB	0  ,8*2		;12
        DB	1*2,8*2		;13
        DB	2*2,8*2		;14
        DB	0  ,0		;15

        MAGLOAD_FILE:
        DS	37
        MAGLOAD_FILE2:
        DS	37

        MAGLOAD_GREEN:	DB	0
        MAGLOAD_RED:	DB	0
        MAGLOAD_BLUE:	DB	0

        MAGID:
        DB	"MAKI02  "
        DB	" X1  "
        DEGI8:
        DB	$00,$00,$00
        DB	$00,$00,$FF
        DB	$00,$FF,$00
        DB	$00,$FF,$FF
        DB	$FF,$00,$00
        DB	$FF,$00,$FF
        DB	$FF,$FF,$00
        DB	$FF,$FF,$FF
        DB	$00,$00,$00
        DB	$00,$00,$FF
        DB	$00,$FF,$00
        DB	$00,$FF,$FF
        DB	$FF,$00,$00
        DB	$FF,$00,$FF
        DB	$FF,$FF,$00
        DB	$FF,$FF,$FF
        MAG_SPBK:
        DW 0